//Запуск скрипта:
//oscript.exe "Telegram отправка картинки.os" ИД_Чата t:\al\ Alt.jpg "Тест картинки"
//
//или
//
//z:\1C\7.7\1Script\bin\oscript.exe "z:\1C\7.7\1Script\bin\Telegram отправка картинки.os" ИД_Чата t:\al\ Alt.jpg "Тест картинки"

Функция Телеграм_ОтправкаКартинки(АйдиЧата,ПутьКФайлуКартинки,ИмяКартинки,ПодписьККартинке="")
	Перем Boundary,Сервер,ИмяФайлаОтправки,ЗаписьДанных,ДвоичныеДанныеИзображения,HTTPЗапрос,HTTPСоединение,ДанныеКакСтрока,АдресЗапроса,Заголовки;
	Если ПустаяСтрока(АйдиЧата) Тогда
		Сообщить("Пустой ИДЧата");
		Возврат 0;
	КонецЕсли;
	Если ПустаяСтрока(ПутьКФайлуКартинки) Тогда
		Сообщить("Пустой путь к картинке");
		Возврат 0;
	КонецЕсли;
	Если ПустаяСтрока(ИмяКартинки) Тогда
		Сообщить("Пустое имя картинки");
		Возврат 0;
	КонецЕсли;


	Boundary = Строка(Новый УникальныйИдентификатор());
	//ОбъединитьФайлы(МассивФайловДляОбъединения, ИмяФайлаОтправки);  //НЕ РАБОТАЕТ! НЕ Реализован метод ОбъединитьФайлы!!

	//Вместо ОбъединитьФайлы (в версих 1С до 8.3.9) сейчас (начиная с 8.3.9) используются:
	//Поток, ПотокВПамяти, ФайловыйПоток
	//
	//https://infostart.ru/public/561328/


		ИмяФайлаОтправки = ПолучитьИмяВременногоФайла("txt");
		Сообщить(ИмяФайлаОтправки);
		ЗаписьДанных = Новый ЗаписьДанных(ИмяФайлаОтправки);

	// ЧАТ
		ЗаписьДанных.ЗаписатьСтроку("--" + Boundary);
		ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""chat_id""");
		ЗаписьДанных.ЗаписатьСтроку("");
		ЗаписьДанных.ЗаписатьСтроку(СокрЛП(АйдиЧата));
	// ПОДПИСЬ
		ЗаписьДанных.ЗаписатьСтроку("--" + Boundary);
		ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""caption""");
		ЗаписьДанных.ЗаписатьСтроку("");
		ЗаписьДанных.ЗаписатьСтроку(СокрЛП(ПодписьККартинке));

	// ФОТО
		ЗаписьДанных.ЗаписатьСтроку("--" + Boundary);
		ЗаписьДанных.ЗаписатьСтроку("Content-Disposition: form-data; name=""photo""; filename="""+ИмяКартинки+"""");
		ЗаписьДанных.ЗаписатьСтроку("Content-Type: image/jpeg");
		ЗаписьДанных.ЗаписатьСтроку("");
		ИмяФайлаКартинки = ПутьКФайлуКартинки+ИмяКартинки;
		ДвоичныеДанныеИзображения = Новый ДвоичныеДанные(ИмяФайлаКартинки);
		ЗаписьДанных.Записать(ДвоичныеДанныеИзображения);
		ЗаписьДанных.ЗаписатьСтроку("");

	// ЗАВЕРШЕНИЕ
		ЗаписьДанных.ЗаписатьСтроку("--" + Boundary + "--");	//Завершение сообщения для сервера
		ЗаписьДанных.ЗаписатьСтроку("");
		ЗаписьДанных.Закрыть();

		TOKENID="Здесь должен быть ваш токен ИД";
		АдресЗапроса="https://api.telegram.org/bot"+TOKENID+"/sendPhoto";

	Заголовки  = Новый Соответствие;
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.Заголовки.Вставить("Connection", "keep-alive");
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "multipart/form-data; boundary="+Boundary);

	HTTPЗапрос.УстановитьИмяФайлаТела(ИмяФайлаОтправки);
	//УстановитьТелоИзСтроки();

	АдресЗапроса="/bot"+TOKENID+"/sendPhoto";
	HTTPЗапрос.АдресРесурса = АдресЗапроса;

	//Сервер = "https://api.telegram.org/"; //В родном 1С-ном методе Нельзя указывать протокол!!!
	//НО по инструкции http://oscript.io/syntax/page/HTTP%D0%A1%D0%BE%D0%B5%D0%B4%D0%B8%D0%BD%D0%B5%D0%BD%D0%B8%D0%B5
	//для реализации https - как раз его и нужно указать в адресе!!!
	Сервер = "https://api.telegram.org";
	HTTPСоединение = Новый HTTPСоединение(Сервер);

	//Сообщить("1. Отправляем запрос:");
	ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
	//Сообщить("2. Отправили запрос:");

	ДанныеКакСтрока = ОтветHTTP.ПолучитьТелоКакСтроку();

	Сообщить("3. Отчет:");
	Сообщить(ДанныеКакСтрока);

	HTTPЗапрос=""; HTTPСоединение="";
КонецФункции


АйдиЧата           = АргументыКоманднойСтроки[0];
ПутьКФайлуКартинки = АргументыКоманднойСтроки[1];
ИмяКартинки        = АргументыКоманднойСтроки[2];
Сообщить("АйдиЧата = "+АйдиЧата);
Сообщить("ПутьКФайлуКартинки = "+ПутьКФайлуКартинки);
Сообщить("ИмяКартинки = "+ИмяКартинки);
Сообщить("АргументыКоманднойСтроки.Количество() = "+АргументыКоманднойСтроки.Количество());
Если АргументыКоманднойСтроки.Количество() = 4 Тогда
	ПодписьККартинке = АргументыКоманднойСтроки[3];
Иначе
	ПодписьККартинке= "";
КонецЕсли;
Сообщить("ПодписьККартинке = "+ПодписьККартинке);

Телеграм_ОтправкаКартинки(АйдиЧата,ПутьКФайлуКартинки,ИмяКартинки,ПодписьККартинке);